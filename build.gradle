
buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
	}
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.bintray'

repositories {
	mavenCentral()
}

group = 'org.standardout'
version = '1.4.0-SNAPSHOT'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

jar {
	// include license into jar
	into 'META-INF', {
		from 'LICENSE'
	}
}

dependencies {
	compile gradleApi()
	compile 'biz.aQute.bnd:bndlib:2.2.0'
//	compile 'org.osgi:org.osgi.core:4.3.0'
	compile 'commons-io:commons-io:2.4'
	compile 'org.eclipse.tycho:org.eclipse.jdt.core:3.11.1.v20150902-1521'
	compile 'de.undercouch:gradle-download-task:2.1.0'
	compile localGroovy()
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}

// package groovydoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'groovydoc') {
	from groovydoc.destinationDir
	classifier = 'javadoc'
}

// package source into a jar file
task packageSources(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

// define artifacts for upload
artifacts {
	archives jar
	archives packageJavadoc
	archives packageSources
}

def configurePom(def pom) {
	// ensure correct artifact ID
	pom.artifactId = 'bnd-platform'
	
	// pom file details
	pom.project {
	   name 'bnd-platform'
	   packaging 'jar'
	   description 'Build OSGi bundles and Eclipse Update Sites from existing JARs, e.g. from Maven repositories (Plugin for Gradle)'
	   url 'https://github.com/stempler/bnd-platform'

	   scm {
		   url 'scm:git:https://github.com/stempler/bnd-platform.git'
		   connection 'scm:git:https://github.com/stempler/bnd-platform.git'
		   developerConnection 'scm:git:https://github.com/stempler/bnd-platform.git'
	   }

	   licenses {
		   license {
			   name 'The Apache Software License, Version 2.0'
			   url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			   distribution 'repo'
		   }
	   }

	   developers {
		   developer {
			   id 'stempler'
			   name 'Simon Templer'
			   email 'simon@templer.cc'
		   }
	   }
   }
}

install {
	repositories.mavenInstaller {
		// ensure correct artifact ID when installing locally
		configurePom(pom)
	}
}

// sign all artifacts
signing {
	required {
		// NOTE: skipping does only work if no gradle properties specifying the key are present
		gradle.taskGraph.hasTask('uploadArchives')
	}
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			// sign artifacts before upload
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
			
			// upload to sonatype OSS (snapshot/release)
			/*
			repository(url: this.version.endsWith('-SNAPSHOT') ?
					'https://oss.sonatype.org/content/repositories/snapshots' :
					'https://oss.sonatype.org/service/local/staging/deploy/maven2') {
				authentication(userName: this.hasProperty('sonatypeUsername') ? sonatypeUsername : '',
					password: this.hasProperty('sonatypePassword') ? sonatypePassword : '')
			}
			*/
			repository(url: mavenLocal().url)
			
			configurePom(pom)
		}
	}
}

// groovydoc task work-around

configurations {
	jansi.extendsFrom(runtime)
}
groovydoc {
	groovyClasspath = project.configurations.jansi
}
dependencies {
	jansi 'org.fusesource.jansi:jansi:1.11'
}

// bintray

bintray { // task bintrayUpload
	user = project.getProperty('bintrayUser')
	key = project.getProperty('bintrayApiKey')

	configurations = ['archives']
	
	dryRun = false
	publish = !project.version.endsWith('-SNAPSHOT')
	pkg {
		def githubUrl = 'https://github.com/stempler/bnd-platform'
		repo = 'gradle-plugins'
		name = 'bnd-platform'
		desc = 'Build OSGi bundles and p2 repositories / Eclipse Update Sites from existing libraries and their dependencies, e.g. from Maven repositories. Useful for instance for creating a target platform for Eclipse/Equinox or Maven Tycho build from third party dependencies.'
		websiteUrl = githubUrl
		issueTrackerUrl = "$githubUrl/issues"
		vcsUrl = "${githubUrl}.git"
		licenses = ['Apache-2.0']
		labels = ['gradle', 'bnd', 'osgi', 'p2', 'eclipse', 'tycho']
		publicDownloadNumbers = true
		// version descriptor
		version {
			name = project.version
			attributes = ['gradle-plugin': "org.standardout.bnd-platform:${project.group}:bnd-platform"]
		}
	}

}

