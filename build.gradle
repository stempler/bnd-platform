plugins {
	id("io.github.gradle-nexus.publish-plugin") version "1.3.0"
	id "signing"
	id "groovy"
	id "eclipse"
	id "maven-publish"
}

repositories {
	mavenCentral()
}

group = 'org.standardout'
version = '2.0.0-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

jar {
	// include license into jar
	into 'META-INF', {
		from 'LICENSE'
	}
}

def isCI = "true".equals(System.getenv("CI"))
def isRelease = !version.endsWith('-SNAPSHOT')

dependencies {
	implementation gradleApi()
	implementation 'biz.aQute.bnd:biz.aQute.bndlib:5.2.0'
	implementation 'org.osgi:osgi.core:6.0.0'
	implementation 'commons-io:commons-io:2.4'
	implementation 'de.undercouch:gradle-download-task:5.5.0'
	implementation localGroovy()
}

tasks.wrapper {
	distributionType = Wrapper.DistributionType.ALL
	gradleVersion = '8.4'
}

// package groovydoc into a jar file
task packageJavadoc(type: Jar, dependsOn: 'groovydoc') {
	from groovydoc.destinationDir
	archiveClassifier = 'javadoc'
}

// package source into a jar file
task packageSources(type: Jar) {
	from sourceSets.main.allSource
	archiveClassifier = 'sources'
}

// groovydoc task work-around

configurations {
	jansi.extendsFrom(runtime)
}
groovydoc {
	groovyClasspath = project.configurations.jansi
}
dependencies {
	jansi 'org.fusesource.jansi:jansi:1.11'
}

nexusPublishing {
	repositories {
		sonatype()
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			groupId "${group}"
			artifactId 'bnd-platform'
			version "${version}"

			artifact packageSources
			artifact packageJavadoc

			pom {
				name = 'bnd-platform'
				packaging = 'jar'
				description = 'Build OSGi bundles and Eclipse Update Sites from existing JARs, e.g. from Maven repositories (Plugin for Gradle)'
				url = 'https://github.com/stempler/bnd-platform'

				scm {
					url = 'scm:git:https://github.com/stempler/bnd-platform.git'
					connection = 'scm:git:https://github.com/stempler/bnd-platform.git'
					developerConnection = 'scm:git:https://github.com/stempler/bnd-platform.git'
				}

				licenses {
					license {
						name = 'The Apache Software License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution = 'repo'
					}
				}

				developers {
					developer {
						id = 'stempler'
						name = 'Simon Templer'
						email = 'simon@templer.cc'
					}
				}
			}
		}
	}
}

// sign all artifacts
signing {
	if (isCI) { // use in ASCII armored key for CI
		def signingKey = findProperty("signingKey")
		def signingPassword = findProperty("signingPassword")
		useInMemoryPgpKeys(signingKey, signingPassword)
	}

	if (isRelease || isCI) {
		// only sign releases or on CI
		sign publishing.publications.mavenJava
	}
}
