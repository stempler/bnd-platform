plugins {
	id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
	id "signing"
	id 'java-gradle-plugin'
	id "groovy"
	id "eclipse"
	id "maven-publish"
	id "com.gradle.plugin-publish" version "1.2.1"
}

repositories {
	mavenCentral()
}

group = 'org.standardout'
version = '3.1.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
	}
}

jar {
	// include license into jar
	into 'META-INF', {
		from 'LICENSE'
	}
}

def isCI = "true".equals(System.getenv("CI"))
def isRelease = !version.endsWith('-SNAPSHOT')

dependencies {
	implementation gradleApi()
	implementation 'biz.aQute.bnd:biz.aQute.bndlib:6.4.1'
	implementation 'org.osgi:osgi.core:8.0.0'
	implementation 'commons-io:commons-io:2.16.1'
	implementation 'de.undercouch:gradle-download-task:5.6.0'
	implementation localGroovy()

	// Testing
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.2'

	testImplementation("org.assertj:assertj-core:3.25.3")
}

test {
	useJUnitPlatform()
}

tasks.wrapper {
	distributionType = Wrapper.DistributionType.ALL
	gradleVersion = '8.7'
}

gradlePlugin {
	def githubUrl = 'https://github.com/stempler/bnd-platform'
    website = githubUrl
    vcsUrl = githubUrl
    plugins {
        bndPlatform {
            id = 'org.standardout.bnd-platform'
            implementationClass = 'org.standardout.gradle.plugin.platform.PlatformPlugin'
            displayName = 'bnd-platform'
            description = 'Build OSGi bundles and p2 repositories / Eclipse Update Sites from existing libraries and their dependencies, e.g. from Maven repositories. Useful for instance for creating a target platform for Eclipse/Equinox or Maven Tycho build from third party dependencies.'
            tags.set(['bnd', 'osgi', 'p2', 'eclipse', 'tycho'])
        }
    }
}

nexusPublishing {
	repositories {
		sonatype()
	}
}

def configurePom(pom) {
	pom.description = 'Build OSGi bundles and Eclipse Update Sites from existing JARs, e.g. from Maven repositories (Plugin for Gradle)'
	pom.url = 'https://github.com/stempler/bnd-platform'

	pom.scm {
		url = 'scm:git:https://github.com/stempler/bnd-platform.git'
		connection = 'scm:git:https://github.com/stempler/bnd-platform.git'
		developerConnection = 'scm:git:https://github.com/stempler/bnd-platform.git'
	}

	pom.licenses {
		license {
			name = 'The Apache Software License, Version 2.0'
			url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			distribution = 'repo'
		}
	}

	pom.developers {
		developer {
			id = 'stempler'
			name = 'Simon Templer'
			email = 'simon@templer.cc'
		}
	}
}

publishing {
	publications {
		pluginMaven(MavenPublication) {
			groupId "${group}"
			artifactId 'bnd-platform'
			version "${version}"

			pom {
				name = 'bnd-platform'
				packaging = 'jar'

				configurePom(pom)
			}
		}
	}

	afterEvaluate {
		publications {
			bndPlatformPluginMarkerMaven {
				pom { pom ->
					configurePom(pom)
				}
			}
		}
	}
}

// sign all artifacts
signing {
	if (isCI) { // use in ASCII armored key for CI
		def signingKey = findProperty("signingKey")
		def signingPassword = findProperty("signingPassword")
		useInMemoryPgpKeys(signingKey, signingPassword)
	}
}

tasks.withType(Sign) {
    onlyIf {
        isRelease || isCI
    }
}
